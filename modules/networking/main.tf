# =============================================================================
# VPC
# =============================================================================
resource "aws_vpc" "main" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true  # requis pour les VPCE avec DNS prive
  enable_dns_hostnames = true  # idem

  tags = {
    Name = "${local.name_prefix}-vpc"
  }
}

# =============================================================================
# Internet Gateway - seule porte de sortie des subnets publics
# =============================================================================
resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "${local.name_prefix}-igw"
  }
}
# =============================================================================
# Subnets publics (1 par AZ)
# =============================================================================
resource "aws_subnet" "public" {
  for_each = local.public_subnets

  vpc_id                  = aws_vpc.main.id
  cidr_block              = each.value
  availability_zone       = each.key
  map_public_ip_on_launch = true

  tags = {
    Name = "${local.name_prefix}-public-${each.key}"
    Tier = "public"
  }
}

# =============================================================================
# Subnets prives app (ASG + EC2 Nextcloud)
# =============================================================================
resource "aws_subnet" "private_app" {
  for_each = local.private_app_subnets

  vpc_id            = aws_vpc.main.id
  cidr_block        = each.value
  availability_zone = each.key

  tags = {
    Name = "${local.name_prefix}-private-app-${each.key}"
    Tier = "private-app"
  }
}

# =============================================================================
# Subnets prives DB (RDS)
# =============================================================================
resource "aws_subnet" "private_db" {
  for_each = local.private_db_subnets

  vpc_id            = aws_vpc.main.id
  cidr_block        = each.value
  availability_zone = each.key

  tags = {
    Name = "${local.name_prefix}-private-db-${each.key}"
    Tier = "private-db"
  }
}
# =============================================================================
# Elastic IP pour la NAT Gateway
# =============================================================================
resource "aws_eip" "nat" {
  domain = "vpc"

  tags = {
    Name = "${local.name_prefix}-nat-eip"
  }

  depends_on = [aws_internet_gateway.main]
}

# =============================================================================
# NAT Gateway (single AZ pour economie)
# =============================================================================
resource "aws_nat_gateway" "main" {
  allocation_id = aws_eip.nat.id

  # NAT dans le subnet public de la premiere AZ
  subnet_id = aws_subnet.public[var.azs[0]].id

  tags = {
    Name = "${local.name_prefix}-nat"
  }

  depends_on = [aws_internet_gateway.main]
}
# =============================================================================
# Route Table publique : 0.0.0.0/0 -> IGW
# =============================================================================
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
  }

  tags = {
    Name = "${local.name_prefix}-public-rt"
  }
}

# =============================================================================
# Route Table privee : 0.0.0.0/0 -> NAT Gateway
# =============================================================================
resource "aws_route_table" "private" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.main.id
  }

  tags = {
    Name = "${local.name_prefix}-private-rt"
  }
}

# =============================================================================
# Associations : 2 publics -> RT publique
# =============================================================================
resource "aws_route_table_association" "public" {
  for_each = aws_subnet.public

  subnet_id      = each.value.id
  route_table_id = aws_route_table.public.id
}

# =============================================================================
# Associations : 2 private_app -> RT privee
# =============================================================================
resource "aws_route_table_association" "private_app" {
  for_each = aws_subnet.private_app

  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}

# =============================================================================
# Associations : 2 private_db -> RT privee
# =============================================================================
resource "aws_route_table_association" "private_db" {
  for_each = aws_subnet.private_db

  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}
# =============================================================================
# VPC Endpoint S3 - gateway, gratuit
# =============================================================================
resource "aws_vpc_endpoint" "s3" {
  vpc_id            = aws_vpc.main.id
  service_name      = "com.amazonaws.eu-west-3.s3"
  vpc_endpoint_type = "Gateway"

  # Injecter la route dans la RT privee
  route_table_ids = [aws_route_table.private.id]

  tags = {
    Name = "${local.name_prefix}-vpce-s3"
  }
}
# =============================================================================
# SG dedie aux VPC endpoints : autorise 443 depuis le VPC CIDR
# =============================================================================
resource "aws_security_group" "vpc_endpoints" {
  name        = "${local.name_prefix}-vpce-sg"
  description = "Autorise HTTPS depuis VPC vers les VPC endpoints"
  vpc_id      = aws_vpc.main.id

  tags = {
    Name = "${local.name_prefix}-vpce-sg"
  }
}

resource "aws_vpc_security_group_ingress_rule" "vpce_https_from_vpc" {
  security_group_id = aws_security_group.vpc_endpoints.id

  description = "HTTPS 443 depuis le VPC"
  from_port   = 443
  to_port     = 443
  ip_protocol = "tcp"
  cidr_ipv4   = var.vpc_cidr
}

# =============================================================================
# VPC Endpoint Secrets Manager - interface, DNS prive
# =============================================================================
resource "aws_vpc_endpoint" "secretsmanager" {
  vpc_id              = aws_vpc.main.id
  service_name        = "com.amazonaws.eu-west-3.secretsmanager"
  vpc_endpoint_type   = "Interface"
  private_dns_enabled = true

  # Deployer le endpoint dans les 2 subnets prives app
  subnet_ids = [for s in aws_subnet.private_app : s.id]

  security_group_ids = [aws_security_group.vpc_endpoints.id]

  tags = {
    Name = "${local.name_prefix}-vpce-secretsmanager"
  }
}

